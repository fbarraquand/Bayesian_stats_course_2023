
    model {

    ########### Priors ################
    mu ~ dnorm(0,0.1)
    tau <- pow(sd,-2)
    sd ~ dunif(0,3)
    
    for(i in 1:M){
      site[i] ~ dnorm(0,tau.site)  # Random site effects
      gamma[i] ~ dnorm(mu.gamma[i],tau.gamma)
      }
    tau.site <- pow(sd.site, -2)
    sd.site ~ dunif(0, 5)
    
    for(t in 1:T){
      year[t] ~ dnorm(0,tau.year) # Random year effects
      }
    tau.year <- pow(sd.year, -2)
    sd.year ~ dunif(0,3)
   
    
    # Other covariate effects on abundance
    for (i in 1:4){
      theta[i] ~ dnorm(0, 0.1)      
    }

    # Priors on trend parameters 
     beta0.mu.gamma ~ dnorm(0, 0.1)   # intercept of mean trend on elev
     beta1.mu.gamma ~ dnorm(0, 0.1)   # lin effect of elev on trend
     beta2.mu.gamma ~ dnorm(0, 0.1)   # quad effect of elev on trend
     tau.gamma <- pow(sd.gamma, -2)
     sd.gamma ~ dunif(0, 0.2)         # Variability of trends (we can discuss that prior - too small?)
    
    ############ Likelihood ###########

    # Linear model for effect of elevation on expectation of trends
    for(i in 1:M){ 
    mu.gamma[i] <- beta0.mu.gamma + beta1.mu.gamma*elev[i] + beta2.mu.gamma*elev[i]^2
       }
     
    for (i in 1:M){
      for(t in 1:T){
        LC[i,t] ~ dnorm(lambda[i,t],tau)
        lambda[i,t] <- mu + site[i] + year[t] + gamma[i] * yr[t] +
        theta[1]*elev[i] + theta[2]*elev[i]^2 + theta[3]*forest[i]+
        theta[4]*dur[i,t] # mixed model
     }
    }

    ############  Derived quantities ############ 
    for (t in 1:T){
      popindex[t] <- sum(lambda[,t])
      }
    }
    